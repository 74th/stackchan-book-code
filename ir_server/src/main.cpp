#include <Arduino.h>
#include <ArduinoJson.h>
#include <Adafruit_NeoPixel.h>
#include <WiFi.h>
#include <WebServer.h>
#include <IRremoteESP8266.h>
#include <IRsend.h>
#include <IRrecv.h>
#include <IRac.h>
#include <IRutils.h>
#include <ssid.hpp>

#ifdef ARDUINO_M5STACK_ATOM_LITE

#define LED_PIN GPIO_NUM_27         // LEDピン

#ifdef USE_UNIT_IR
#define IR_SEND_PIN GPIO_NUM_26     // M5 IR赤外線送信ピン(Grove D2)
#else
#define IR_SEND_PIN GPIO_NUM_12     // ATOM Liteの赤外線送信ピン
#endif
#define IR_RECEIVER_PIN GPIO_NUM_32 // 赤外線受信ピン

#elif defined(IOT_SERVER_BOARD)

#define LED_PIN GPIO_NUM_2         // LEDピン
#define IR_SEND_PIN GPIO_NUM_5     // M5 IR赤外線送信ピン(Grove D2)
#define IR_RECEIVER_PIN GPIO_NUM_4 // 赤外線受信ピン

#endif

// LEDの色の定義
#define LED_COLOR_GREEN Adafruit_NeoPixel::Color(0, 32, 0)
#define LED_COLOR_BLUE Adafruit_NeoPixel::Color(0, 0, 32)
#define LED_COLOR_YELLOW Adafruit_NeoPixel::Color(32, 32, 0)

// LEDの定義（数、制御ピン、仕様）
Adafruit_NeoPixel led(1, LED_PIN, NEO_GRB + NEO_KHZ800);

// SSID、パスワードの文字列化
const char *ssid = WIFI_SSID_H;
const char *pass = WIFI_PASSWORD_H;
;

// 赤外線送信用オブジェクト
IRsend irsend(IR_SEND_PIN);

// 赤外線受信オブジェクト
// 引数: ピン番号、バッファサイズ、タイムアウト(ms)、デコード時に二次バッファを使うか
IRrecv irrecv(IR_RECEIVER_PIN, 1024, 50, true);

WebServer server(80);

// hexの文字列をuint64_tに変換
uint64_t hexStringToUint64(const char *hexString)
{
    if (hexString[0] == '0' && (hexString[1] == 'x' || hexString[1] == 'X'))
    {
        hexString += 2;
    }

    uint64_t result = strtoull(hexString, NULL, 16);

    return result;
}

// 16進数文字を対応する数値に変換
unsigned char hexCharToValue(char hexChar)
{
    if ('0' <= hexChar && hexChar <= '9')
    {
        return hexChar - '0';
    }
    else if ('a' <= hexChar && hexChar <= 'f')
    {
        return hexChar - 'a' + 10;
    }
    else if ('A' <= hexChar && hexChar <= 'F')
    {
        return hexChar - 'A' + 10;
    }
    else
    {
        return 0;
    }
}

// 文字が16進数として有効か判定
bool isHexChar(char hexChar)
{
    return ('0' <= hexChar && hexChar <= '9') || ('a' <= hexChar && hexChar <= 'f') || ('A' <= hexChar && hexChar <= 'F');
}

// HEX文字列をバイト列に変換する関数
void hexStringToByteArray(const char *hexString, unsigned char *byteArray, size_t *byteArraySize)
{
    if (hexString[0] == '0' && (hexString[1] == 'x' || hexString[1] == 'X'))
    {
        hexString += 2;
    }

    size_t hexLen = strlen(hexString);
    *byteArraySize = hexLen / 2;

    for (size_t i = 0; i < hexLen / 2; ++i)
    {
        byteArray[i] = (hexCharToValue(hexString[2 * i]) << 4) | hexCharToValue(hexString[2 * i + 1]);
    }
}

// uint16_t配列をHEX文字列に変換する関数
String uint16ArrayToHexString(const uint16_t *values, size_t valuesLength)
{
    String hexString = "0x";
    hexString.reserve(2 + valuesLength * 4);

    char buf[5];
    for (size_t i = 0; i < valuesLength; ++i)
    {
        snprintf(buf, sizeof(buf), "%04X", values[i]);
        hexString += buf;
    }

    return hexString;
}

// HEX文字列のuint16_t要素数を取得する関数
bool getUint16ArrayLengthFromHexString(const char *hexString, size_t *valuesLength)
{
    if (hexString == NULL)
    {
        return false;
    }

    if (hexString[0] == '0' && (hexString[1] == 'x' || hexString[1] == 'X'))
    {
        hexString += 2;
    }

    size_t hexLen = strlen(hexString);
    if (hexLen == 0 || hexLen % 4 != 0)
    {
        return false;
    }

    for (size_t i = 0; i < hexLen; ++i)
    {
        if (!isHexChar(hexString[i]))
        {
            return false;
        }
    }

    *valuesLength = hexLen / 4;
    return true;
}

// HEX文字列をuint16_t配列に変換する関数
bool hexStringToUint16Array(const char *hexString, uint16_t *values, size_t valuesLength)
{
    size_t parsedLength;
    if (!getUint16ArrayLengthFromHexString(hexString, &parsedLength) || parsedLength != valuesLength)
    {
        return false;
    }

    if (hexString[0] == '0' && (hexString[1] == 'x' || hexString[1] == 'X'))
    {
        hexString += 2;
    }

    for (size_t i = 0; i < valuesLength; ++i)
    {
        values[i] = (hexCharToValue(hexString[i * 4]) << 12) |
                    (hexCharToValue(hexString[i * 4 + 1]) << 8) |
                    (hexCharToValue(hexString[i * 4 + 2]) << 4) |
                    hexCharToValue(hexString[i * 4 + 3]);
    }

    return true;
}

// GET /
void handleRootAPI()
{
    Serial.println("access GET /");
    server.send(200, "application/json", "{}");
}

// 404 Not Found
void handleNotFoundAPI()
{
    server.send(404, "application/json", "{\"message\": \"Not Found.\"}");
}

// POST /ir/send
void handleIRSendAPI()
{
    Serial.println("access POST /ir/send");

    // 処理中を色で表示
    led.setPixelColor(0, LED_COLOR_YELLOW);
    led.show();

    JsonDocument resDoc;       // レスポンスのJSON
    JsonDocument reqDoc;       // リクエストのJSON
    char resBodyBuf[1024 * 2]; // レスポンスのバッファ

    // リクエストボディのJSONデシリアライズ
    String requestBody = server.arg("plain");
    Serial.print("request body:");
    Serial.println(requestBody);
    DeserializationError err = deserializeJson(reqDoc, requestBody);
    if (err)
    {
        // デシリアライズ失敗した場合のエラー
        resDoc["error"] = "failed to deserialize Json";
        resDoc["error_detail"] = err.c_str();
        resDoc["success"] = false;

        // JSONをレスポンス
        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        // 処理終了のLED表示
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    // 信号のタイプのプロパティを取得
    const char *type = reqDoc["type"];
    if (type == NULL || strlen(type) == 0)
    {
        // プロパティがないエラー
        resDoc["error"] = "type is required.";
        resDoc["success"] = false;

        // JSONをレスポンス
        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        // 処理終了のLED表示
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    // 信号データのプロパティを取得
    const char *hexData = reqDoc["hex"];
    if (hexData == NULL || strlen(hexData) == 0)
    {
        // 取得できない場合エラーを返す
        String errorMessage = "hex is required.";
        resDoc["error"] = errorMessage;
        resDoc["success"] = false;
        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(errorMessage);
        server.send(401, "application/json", resBodyBuf);

        // 処理終了のLED表示
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    // タイプごとに送信
    if (strcmp(type, "SONY") == 0)
    {
        // SONY TV

        // hexテキストをuint64_tにパース
        uint64_t data_u64 = hexStringToUint64(hexData);
        Serial.printf("data: %x\r\n", data_u64);

        // データ長を特定（SONYは、12、15、20ビットの3種類）
        int16_t dataSize_bits;
        if (data_u64 <= 1 << 12)
        {
            dataSize_bits = 12;
        }
        else if (data_u64 <= 1 << 15)
        {
            dataSize_bits = 15;
        }
        else
        {
            dataSize_bits = 20;
        }

        // 送信
        irsend.sendSony(data_u64, dataSize_bits, 2);
    }
    else if (strcmp(type, "MITSUBISHI_AC") == 0)
    {
        // 三菱エアコン
        unsigned char data_bytes[64];
        size_t dataSize_bytes;

        // hexデータ
        hexStringToByteArray(hexData, data_bytes, &dataSize_bytes);
        Serial.printf("data: %llx\r\n", data_bytes);

        // 送信
        irsend.sendMitsubishiAC(data_bytes, dataSize_bytes);
    }
    else
    {
        // 不明なタイプのエラーレスポンス
        resDoc["error"] = "unknown type";
        resDoc["success"] = false;

        // JSONをレスポンス
        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        // 処理終了
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    // 成功時の応答のレスポンス
    resDoc["success"] = true;

    // JSONをレスポンス
    serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
    Serial.println(resBodyBuf);
    server.send(200, "application/json", resBodyBuf);

    // 処理終了
    led.setPixelColor(0, LED_COLOR_BLUE);
    led.show();
}

// POST /ir/send_raw
void handleIRSendRawAPI()
{
    Serial.println("access POST /ir/send_raw");

    // 処理中を色で表示
    led.setPixelColor(0, LED_COLOR_YELLOW);
    led.show();

    JsonDocument resDoc;       // レスポンスのJSON
    JsonDocument reqDoc;       // リクエストのJSON
    char resBodyBuf[1024 * 2]; // レスポンスのバッファ

    // リクエストボディのJSONデシリアライズ
    String requestBody = server.arg("plain");
    Serial.print("request body:");
    Serial.println(requestBody);
    DeserializationError err = deserializeJson(reqDoc, requestBody);
    if (err)
    {
        // デシリアライズ失敗した場合のエラー
        resDoc["error"] = "failed to deserialize Json";
        resDoc["error_detail"] = err.c_str();
        resDoc["success"] = false;

        // JSONをレスポンス
        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        // 処理終了のLED表示
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    uint16_t frequency = reqDoc["frequency"] | 38;
    const char *rawHex = reqDoc["data"]["raw"];
    if (rawHex == NULL || strlen(rawHex) == 0)
    {
        resDoc["error"] = "data.raw is required.";
        resDoc["success"] = false;

        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    size_t rawDataLength;
    if (!getUint16ArrayLengthFromHexString(rawHex, &rawDataLength))
    {
        resDoc["error"] = "data.raw must be a hex string of uint16 values.";
        resDoc["success"] = false;

        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    uint16_t *rawData = (uint16_t *)malloc(sizeof(uint16_t) * rawDataLength);
    if (rawData == NULL)
    {
        resDoc["error"] = "failed to allocate raw buffer";
        resDoc["success"] = false;

        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(500, "application/json", resBodyBuf);

        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    if (!hexStringToUint16Array(rawHex, rawData, rawDataLength))
    {
        free(rawData);

        resDoc["error"] = "failed to parse data.raw.";
        resDoc["success"] = false;

        serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
        Serial.println(resBodyBuf);
        server.send(401, "application/json", resBodyBuf);

        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        return;
    }

    irsend.sendRaw(rawData, rawDataLength, frequency);
    free(rawData);

    // 成功時の応答のレスポンス
    resDoc["success"] = true;

    // JSONをレスポンス
    serializeJson(resDoc, resBodyBuf, sizeof(resBodyBuf));
    Serial.println(resBodyBuf);
    server.send(200, "application/json", resBodyBuf);

    // 処理終了
    led.setPixelColor(0, LED_COLOR_BLUE);
    led.show();
}


// GET /ir/receive
void handleIRReceiveAPI()
{
    Serial.println("access GET /ir/receive");

    // 処理中を色で表示
    led.setPixelColor(0, LED_COLOR_YELLOW);
    led.show();

    JsonDocument resDoc; // レスポンスのJSON
    String resBody;      // レスポンスのバッファ

    uint32_t start_time = millis();
    while (true)
    {
        if (millis() > start_time + 10000)
        {
            // タイムアウトのエラー
            resDoc["success"] = false;
            resDoc["error"] = "timeout";

            // JSONをレスポンス
            resBody = "";
            serializeJson(resDoc, resBody);
            Serial.println(resBody);
            server.send(400, "application/json", resBody);

            // 処理終了のLED表示
            led.setPixelColor(0, LED_COLOR_BLUE);
            led.show();
            break;
        }

        // 受信デコード結果
        decode_results results;
        if (!irrecv.decode(&results))
        {
            // 受信できていない
            continue;
        }

        // 受信、デコード結果をシリアルに出力
        Serial.println("received:");
        Serial.println(resultToHumanReadableBasic(&results));

        uint16_t *raw = resultToRawArray(&results);
        uint16_t len = getCorrectedRawLength(&results);
        if (raw == NULL)
        {
            resDoc["success"] = false;
            resDoc["error"] = "failed to allocate raw buffer";

            resBody = "";
            serializeJson(resDoc, resBody);
            Serial.println(resBody);
            server.send(500, "application/json", resBody);

            led.setPixelColor(0, LED_COLOR_BLUE);
            led.show();
            break;
        }

        // 受信、デコード結果をJSON形式でレスポンス
        resDoc["data"]["type"] = typeToString(results.decode_type, results.repeat);
        resDoc["data"]["hex"] = resultToHexidecimal(&results);
        resDoc["data"]["raw"] = uint16ArrayToHexString(raw, len);
        resDoc["success"] = true;

        // JSONをレスポンス
        resBody = "";
        serializeJson(resDoc, resBody);
        Serial.println(resBody);
        server.send(200, "application/json", resBody);

        if (raw != NULL)
        {
            delete[] raw;
        }

        // 処理終了のLED表示
        led.setPixelColor(0, LED_COLOR_BLUE);
        led.show();
        break;
    }
}

void setup()
{
    Serial.begin(115200);

    // LED
    led.begin();

    // セットアップ中は緑色にする
    led.setPixelColor(0, LED_COLOR_GREEN);
    led.show();

    // 赤外線送信の初期化
    irsend.begin();

    // 赤外線受信の初期化
    irrecv.setUnknownThreshold(12);
    irrecv.setTolerance(kTolerance);
    irrecv.enableIRIn();

    Serial.print("setup wifi.");
    WiFi.begin(ssid, pass);
    while (WiFi.status() != WL_CONNECTED)
    {
        Serial.print(".");
        delay(500);
    }
    Serial.println("connected");
    Serial.print("IP:");
    Serial.println(WiFi.localIP());

    // Webサーバの開始
    server.on("/", HTTP_GET, handleRootAPI);
    server.on("/ir/send", HTTP_POST, handleIRSendAPI);
    server.on("/ir/send_raw", HTTP_POST, handleIRSendRawAPI);
    server.on("/ir/receive", HTTP_GET, handleIRReceiveAPI);
    server.onNotFound(handleNotFoundAPI);
    server.begin();

    // セットアップ後は青色にする
    led.setPixelColor(0, LED_COLOR_BLUE);
    led.show();
}

// 最後のモニター出力
uint32_t last_monitor_time = 0;

void loop()
{
    if (millis() - last_monitor_time > 1000)
    {
        // モニター出力を1秒ごとにする
        last_monitor_time = millis();
        Serial.print("IP:");
        Serial.println(WiFi.localIP());
    }

    // サーバのリクエストを処理する
    server.handleClient();
}
